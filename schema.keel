role Sailors {
    domains {
        "keel.xyz"
    }
}

enum Gender {
    Male
    Female
    Nonbinary
    Other
}

enum Region {
    Asia
    Americas
    Africa
    Mena
    Europe
    Poles
    Oceania
}

job FetchNewBooks {
    @schedule("*/5 * * * *")
    @permission(roles: [Sailors])
}

model Book {
    fields {
        isbn Text
        title Text?
        author Text?
        published Date?
        owner Identity?
    }

    operations {
        get book(id) {
            @permission(expression: true)
        }
        list allBooks() {
            @permission(expression: true)
        }
        delete deleteBook(id)
        update updateBook(id) with (isbn?, author?, published?, title?)
        create createBook() with (isbn, author?, published?, title?) {
            @set(book.owner = ctx.identity)
            @permission(expression: true)
        }
    }

    functions {
        create createWithMagic() with (isbn)
    }

    @permission(
        expression: ctx.isAuthenticated,
        actions: [create]
    )

    @permission(
        roles: [Sailors],
        actions: [get, update, delete, list, create]
    )
}
